rule Turla_LNTA_v1 {
   meta:
      description = "Detect Turla LightNeuron Transport Agent"
	    author = "Emanuele De Lucia - Telsy SpA - thanks to @TS_WAY_SRL for cooperation"
	    tlp = "white"
   strings:
      $x1 = "networkservice\\appdata\\local\\temp\\tmp1197.tmp" fullword wide
      $x2 = "networkservice\\appdata\\local\\temp\\tmp8621.tmp" fullword wide
      $s1 = "BPA.Transport.dll" fullword wide
      $s2 = "Microsoft.Exchange.MessagingPolicies.Search.exe" fullword wide
      $s3 = "Microsoft.Exchange.Data.Transport.Smtp" fullword ascii
      $s4 = "Microsoft.Exchange.Data.Transport.Routing" fullword ascii
      $s5 = "Microsoft.Exchange.Data.Transport" fullword ascii
      $s6 = "Microsoft.Exchange.Data.Transport.Email" fullword ascii
      $s7 = "Microsoft.Exchange.Data.Common" fullword ascii
      $s8 = "<PrivateImplementationDetails>{02B053A4-60A4-4EDE-A573-00A8BD84A5D3}" fullword ascii
      $s9 = "JournalReportAgentFactory" fullword ascii
      $s10 = "Microsoft.Exchange.Data.Mime" fullword ascii
      $s11 = "JournalReportAgent" fullword ascii
      $s12 = "GetMimeReadStream" fullword ascii
      $s13 = "Microsoft.Exchange.MessagingPolicies.Search.Properties" fullword ascii
      $s14 = "BinaryLogEx" fullword wide
      $s15 = "Microsoft.Exchange.MessagingPolicies.Search" fullword ascii
      $s16 = "MessagingRoutingAgentFactory" fullword ascii
      $s17 = "templatorLoaded" fullword ascii
      $s18 = "templatorName" fullword ascii
   condition:
      uint16(0) == 0x5a4d and filesize < 40KB and (1 of ($x*) and 5 of ($s*) )
}
